LABTITLE=: 'JOD (2) Source Code Dump Scripts'
LABAUTHOR=: 0 : 0
John D. Baker
bakerjd99@gmail.com
)
LABCOMMENTS=: 'This lab demonstrates how to load dump JOD scripts - revised for J 8.0x March 2018'
LABFOCUS=: 1

NB. =========================================================
Lab Section Introduction
JOD source code is distributed in three JOD dump scripts. Dump scripts are plain ASCII J scripts that can be loaded with J's standard script processing verbs.

This lab assumes JOD and supporting addons have been installed. JOD and supporting addons can be installed with the J package manager.
)
NB. to use a dump script JOD must be active
require 'general/jod'

NB. =========================================================
Lab Section Configure a ~jodsource directory
To run a JOD dump script simply open a new dictionary and use (0!:0). 

For this lab the source dictionaries are created in the (~JODSOURCE) relative directory.  You will need to configure a J folder to point to an actual directory. I STRONGLY RECOMMEND using a directory that is not in the J system tree! You don't want your precious dictionary databases being wiped by installing the next J version.

Before proceeding configure a (~JODSOURCE) directory. On J8.0x JQT systems the Configure/Folders menu opens the directory folder configuration file.
)
NB. configure a ~JODSOURCE directory before proceeding to the next step.

NB. =========================================================
Lab Section Remove previous lab dictionaries.
WARNING: THE NEXT STEP REMOVES DICTIONARIES CREATED BY PRIOR RUNS OF THIS LAB.  THE DICTIONARIES (utilsload, jodload, joddevload) WILL BE DELETED.  IF YOU CARE ABOUT THE CONTENTS OF THESE DICTIONARIES DO NOT EXECUTE THE NEXT LAB STEP.

This step defines a verb that removes lab source dictionaries.
)
RemoveLabSourceDictionaries_ijod_=: 3 : 0
if. '~JODSOURCE' -: root=. jpath '~JODSOURCE' do.
  smoutput 'A JODSOURCE directory is not setup - setup and try again.'
elseif. -.fexist jpath '~addons/general/jodsource/jod.ijs' do.
  smoutput 'JODSOURCE addon is not installed - install and try again.'
elseif. IFWIN do.
  NB. clear out dictionary directories
  shell 'rd /s /q "',(root,'\utilsload'),'"'
  shell 'rd /s /q "',(root,'\jodload'),'"'
  shell 'rd /s /q "',(root,'\joddevload'),'"'
  smoutput 'Lab temporary (win) source dictionaries erased'
elseif. IFUNIX do.
  NB. avoid blanks in JODSOURCE paths on Linux and Mac systems
  shell 'rm -rf ',root,'/utilsload'
  shell 'rm -rf ',root,'/jodload'
  shell 'rm -rf ',root,'/joddevload'
  smoutput 'Lab temporary (mac/linux) source dictionaries erased'
elseif.do.
  smoutput 'Erase any previous temporary lab source dictionaries manually.'
end.
)

NB. =========================================================
Lab Section
Remove any previous load lab dictionaries.
)
NB. unregister - ignore errors
0 0 $ 3 regd&> ;:'utilsload jodload joddevload' [ 3 od ''

NB. clear out dictionary directories
RemoveLabSourceDictionaries 0

NB. =========================================================
Lab Section Create utilsload dictionary
Dump scripts are loaded into dictionaries.  Create a (utilsload) dictionary.
)
NB. create a new utils directory in (~JODSOURCE) - it will be renamed later
NB. You must configure (~JODSOURCE) and point to an actual empty directory.

newd 'utilsload';(jpath '~JODSOURCE/utilsload');'main utility dictionary'

NB. close any open dictionaries and open
od 'utilsload' [ 3 od ''

NB. =========================================================
Lab Section
When JOD dump scripts run they do not disturb your current J environment.  All words are defined in a numbered scratch locale that is created when JOD loads.  JOD dump scripts display messages after loading batches of objects.  The next lab step loads the utility dictionary.

Warning: dump scripts can be very large so it's a good idea to use always use (0!:x).  Loading a dump script into J session editors can crash J for very large scripts.
)

NB. =========================================================
Lab Section
Load the utilities dictionary.
)
NB. run the utils dump script
0!:0 <jpath '~addons/general/jodsource/utils.ijs'

NB. =========================================================
Lab Section Regenerate utils cross references
The last message displayed by a dump script is a reminder to regenerate cross references.  JOD dictionaries maintain word cross references that are used in many ways.  The cross references are not stored in dumps and must be recreated afterwards.
)
NB. regenerate utilities cross references and show first 5 messages
5 {. 0 globs&> }. revo ''


NB. make utilities a read only dictionary
dpset 'READONLY'

NB. =========================================================
Lab Section Load jod
The utilities dictionary is now loaded and cross references have been updated.  To complete the restoration of JOD source code we now create and load (jodload) and (joddevload).
)
NB. create dictionaries
newd 'jodload';(jpath '~JODSOURCE/jodload');'jod source code dictionary'
newd 'joddevload';(jpath '~JODSOURCE/joddevload');'jod development put dictionary'

NB. =========================================================
Lab Section
JOD source code depends on objects in the utilities dictionary.  This relationship is maintained by the path order of dictionaries.
)
NB. close any open dictionaries 
3 od ''

NB. open (jodload) and (utilsload)
od ;:'jodload utilsload'

NB. display dictionary information
did~  0

NB. =========================================================
Lab Section
Load JOD source code.  The JOD dictionary dump is large because it contains a substantial amount of documentation and test data. It may take a few moments to load.
)
NB. load jod
0!:0 <jpath '~addons/general/jodsource/jod.ijs'

NB. =========================================================
Lab Section Regenerate jod cross references
Before regenerating jod's cross references look at the open dictionaries.  The (jodload) dictionary now has a reference path that includes (utilsload).
)
NB. note the (jodload) path
did~ 0

NB. =========================================================
Lab Section
JOD cross references will depend on utilities.
)
NB. regenerate (jodload) cross references and show first 5 messages
5 {. 0 globs&> }. revo ''

NB. make (jodload) a read only dictionary
dpset 'READONLY'

NB. =========================================================
Lab Section Load joddev
Now let's load and cross reference (joddevload).  (joddevload) is what I call the "dev-layer".  The dev-layer is a put dictionary that sits on top of a number of stable dictionaries. During development only the dev-layer changes.  You can easily revoke a dev-layer dictionary by closing it.
)
NB. close open dictionaries and open in correct path order
3 od ''
od ;:'joddevload jodload utilsload'

NB. =========================================================
Lab Section
Load and cross reference.
)
NB. load (joddevload)
0!:0 <jpath '~addons/general/jodsource/joddev.ijs'

NB. insure at least one group exists - defines put dictionary path
grp 'jodon';}. grp 'jodon'

NB. cross reference
5 {. 0 globs&> }. revo '' 

NB. development path dictionaries
did~ 0

NB. =========================================================
Lab Section Generate fully commented jod scripts
The JOD source code dictionaries have now been restored. 

To generate fully commented traditional J script files for the components of the JOD system you can run a script stored in the JOD dictionaries.  This step runs this script.
)
NB. generate JOD script files in fully commented form.
rm 'buildjodsource'

NB. close dictionaries
3 od ''

NB. =========================================================
Lab Section Rename JOD load dictionaries.
The JOD source code dictionaries are named (joddev) (jod) and (utils).  Many test and build scripts in these dictionaries make use of these names.  To dive into JOD code it's a good idea to copy and rename the load dictionaries. Uncomment and run the following lines to copy and rename the load dictionaries. 

WARNING: UNCOMMENT ONLY IF YOU DO NOT ALREADY HAVE (joddev) (jod) AND (utils) DICTIONARIES.
)
NB. unregister
NB. 3 regd&> ;:'utilsload jodload joddevload utils jod joddev'

NB. clear any jod source dictionaries
NB. Note: commands are Windows oriented. For unix systems
NB. remove and copy directories manually.
NB. shell 'rd /s /q "',(jpath '~JODSOURCE\utils'),'"'
NB. shell 'rd /s /q "',(jpath '~JODSOURCE\jod'),'"'
NB. shell 'rd /s /q "',(jpath '~JODSOURCE\joddev'),'"'

NB. copy dictionaries - /e option copies required empty subdirectories 
NB. shell 'xcopy "',(jpath '~JODSOURCE\utilsload'),'" "',(jpath '~JODSOURCE\utils\'),'" /e /y'
NB. shell 'xcopy "',(jpath '~JODSOURCE\jodload'),'" "',(jpath '~JODSOURCE\jod\'),'" /e /y'
NB. shell 'xcopy "',(jpath '~JODSOURCE\joddevload'),'" "',(jpath '~JODSOURCE\joddev\'),'" /e /y'

NB. re-register with new names
NB. regd 'utils';(jpath '~JODSOURCE\utils');'general utilities'
NB. regd 'jod';(jpath '~JODSOURCE\jod');'jod source code'
NB. regd 'joddev';(jpath '~JODSOURCE\joddev');'jod development'

NB. =========================================================
Lab Section Summary
This lab showed how to load dump scripts.  JOD source code dictionaries were loaded.  To use the JOD source dictionaries make sure they are named (joddev), (jod) and (utils).

John Baker
March 2018
bakerjd99@gmail.com
)

